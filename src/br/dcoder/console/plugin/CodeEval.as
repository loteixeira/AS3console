// AS3Console Copyright 2011 Lucas Teixeira (aka Disturbed Coder)
// Project page: https://github.com/loteixeira/as3console
//
// This software is distribuited under the terms of the GNU Lesser Public License.
// See license.txt for more information.

package br.dcoder.console.plugin
{
	import br.dcoder.console.*;
	
	import com.hurlant.eval.CompiledESC;
	import com.hurlant.eval.ByteLoader;
	
	import flash.display.Loader;
	import flash.events.Event;
	import flash.utils.ByteArray;
	import flash.utils.getDefinitionByName;

	public class CodeEval
	{
		private static var _context:Object;
		private static var esc:CompiledESC = new CompiledESC();
		
		/**
		 * Start CodeEval plugin. Using this plugin every time you type "eval [source code]" on console input text field,
		 * [source code] block will be compiled to AVM2 bytecode and executed.
		 * Generated byte code runs from inside a function, where receive two parameters:<br>
		 * <li>getDefinition:Function -> alias for flash.utils.getDefinitionByName</li>
		 * <li>context:Object -> context object</li>
		 * @param context An acessible object for evaluated source code
		 */
		public static function start(_context:Object = null):void
		{
			CodeEval._context = _context;
			Console.instance.addEventListener(ConsoleEvent.INPUT, input);
		}
		
		/**
		 * Context object getter and setter.
		 */
		public static function get context():Object
		{
			return _context;
		}
		
		public static function set context(_context:Object):void
		{
			CodeEval._context = _context;
		}

		/**
		 * @private
		 */
		private static function input(event:ConsoleEvent):void
		{
			if (event.text.indexOf("eval ") == 0)
			{
				var src:String = "function evalCode(getDefinition:Function, context:Object):void { " + event.text.substr(5) + " }";
				var swf:ByteArray = ByteLoader.wrapInSWF([ esc.eval(src) ]);
				var loader:Loader = new Loader();

				loader.contentLoaderInfo.addEventListener(Event.INIT, function(event:Event):void
				{
					try
					{
						var getDefinition:Function = function(expression:String):* { return getDefinitionByName(expression); };
						loader.contentLoaderInfo.applicationDomain.getDefinition("evalCode")(getDefinition, _context);
					}
					catch (e:Error)
					{
						cpln("Error executing code:");
						cpln(e);
					}
				});
			
				loader.loadBytes(swf);
				event.stopImmediatePropagation();
			}
		}
	}
}
